---
title : Accelerated sotalol dosing stratgies
author : Elyes Dahmane, Vijay Ivaturi
date :  August 10, 2019
---

```julia
using Pumas, LinearAlgebra, Plots
```

```julia
using Random
Random.seed!(1)
```

This tutorial will demonstrate sotalol accelerated dosing strategies to decrease length of hospital stay after sotalol initiation. The original article is
published [here](https://www.ncbi.nlm.nih.gov/pubmed/31328888)

## Background

Oral Sotalol used in adults for sinus rhythm-control is initiated at 80 mg BID (two times a day) and titrated to a maximum safe dose.
FDA recommends QTc (corrected QT interval) monitoring for at least 3 days, until steady-state (SS) exposure of the drug is reached, before patient discharge, which can significantly impact the total cost of treatment.

The primary goal of this work was to develop a dosing strategy that uses the intravenous formulation of sotalol along with the oral dose to reduce the hospital length of stay.

The following information is already available from the [FDA clnical pharmacology reviews]():

1. Sotalol pharmacokinetic (PK) model
2. Relation between sotalol pharmacokinetics and QTc prolongation (PKPD)

The PKPD model for sotalol was then translated to Pumas

```julia
sotalol_PKPD_model = @model begin

    @param   begin
        θ ∈ VectorDomain(9)
        Ω ∈ PDiagDomain(5)
        σ_prop_PK ∈ RealDomain(init= 0.02)
        σ_prop_PD ∈ RealDomain(init = 0.02)
    end

    @random begin
      η ~ MvNormal(Ω)
    end

    @pre begin
        bioav = (Central = θ[1],)
        Ka = θ[2]*exp(η[1])
        lags = (Depot = θ[3],)
        CL = θ[4]*(BW/70)^0.75*(0.5)^CKI*exp(η[2])
        Vc  = θ[5]*(BW/70)*exp(η[3])
        Q  = θ[6]*(BW/70)^0.75
        Vp  = θ[7]*(BW/70)

        e0 = θ[8]*exp(η[4])
        Slope = θ[9]*exp(η[5])
    end

    @covariates BW CKI

    @dynamics begin
        Depot'   = -Ka*Depot
        Central' =  Ka*Depot - (CL/Vc)*Central - (Q/Vc)*Central + (Q/Vp)*Peripheral
        Peripheral' = (Q/Vc)*Central - (Q/Vp)*Peripheral
    end

    @derived begin
        cp = @. (Central / (Vc/1000))
        dv = @. Normal(cp, sqrt(cp^2*σ_prop_PK))
        qtc = @. e0 + (Slope*cp)
        qt = @. Normal(qtc, sqrt(σ_prop_PD))
    end

end
```

The parameters of the model are defined below

```julia
parameters_PKPD = ( θ =     [1.0667, #bioav
                            0.605, #Ka
                            0.231, #alag1
                            12, #CL
                            77.1, #Vc
                            9.22, #Q
                            52.3, #Vp
                            405, #E0
                            0.0158 #Slope
                            ],
                    Ω = Diagonal([0.4692, 0.0198, 0.0713, 0.0025, 0.034]),
                    σ_prop_PK = 0.06, # made it smaller because it was ridiculously high 0.2285 (%CV of 47.8),
                    σ_prop_PD = 19.1
                    )
```

```julia
randeffs = (η = zeros(9),)
```

## Traditional dosing strategy and time to stable QTc

Given the model above, let us first simulate a traditional dosing strategy that is used to initiate patients on sotalol -  80 mg BID

**set up the dosing regiment**

```julia
reg_PO_80 = DosageRegimen(80, cmt=1, time=0, ii=12, addl=5, rate=0)
reg_PO_80_CKI = DosageRegimen(80, cmt=1, time=0, ii=24, addl=5, rate=0)
```

**create a single subject**

```julia
s1 = Subject(id=1, evs=reg_PO_80, cvs = (BW = 70, CKI=0))
```

```julia
bidpo=simobs(sotalol_PKPD_model, s1, parameters_PKPD,randeffs)
plot(bidpo, obsnames = [:cp,:qtc],
    color=[2 3],lw=2,
    xticks = 0:12:96, title = "Subject 1 - 80 mg PO")
```

The top panel shows that it takes about 5-6 doses or 3 days to reach steady-state concentrations of sotalol in plasma.
Similarly, it takes abuot 3 days for the QTc to stabilize to its new steady-state value.


The approved bioequivalent intravenous dose of sotalol (75 mg) on the other hand is given as a 5-hour infusion that
is tedious.

```julia
reg_IV_75 = DosageRegimen(75,cmt=2,time=0,rate=75/5, ii=12, addl=5)
s2 = Subject(id=1, evs=reg_IV_75, cvs = (BW = 70, CKI=0))
```

```julia
bidiv=simobs(sotalol_PKPD_model, s2, parameters_PKPD, randeffs)
plot(bidiv, obsnames = [:cp,:qtc],
    color=[2 3],lw=2,
    xticks = 0:12:96, title = "Subject 2 - 75 mg IV")
```

Using either of IV or PO formulations requires three days of hospital stay.

## Proposed alternate dossing regimen with mixed IV and PO administration

While we tested multiple IV loading doses and combinations with oral doses, the table
below best describes the final recommendations for each target oral dose broken down by
kidney function.

| GFR            | Target Oral Dose | IV Loading dose | Maintenance PO dose |
|----------------|------------------|-----------------|---------------------|
| > 60 mL/min    | 80 mg BID        | 40 mg / 1h      | 80 mg BID           |
|                | 120 mg BID       | 40 mg / 1h +    | 120 mg BID          |
|                |                  | 20 mg / 0.5h    |                     |
|                | 160mg BID        | 40 mg / 1h +    | 160mg BID           |
|                |                  | 20 mg / 0.5h +  |                     |
|                |                  | 20 mg / 0.5h    |                     |
| 60 - 40 mL/min | 80 mg QD         | 40 mg / 1h      | 80 mg QD            |
|                | 120 mg QD        | 40 mg / 1h +    | 120 mg QD           |
|                |                  | 10 mg / 0.5h    |                     |
|                | 160mg QD         | 40 mg / 1h +    | 160mg QD            |
|                |                  | 10 mg / 0.5h +  |                     |
|                |                  | 20 mg / 0.5h    |                     |


### Setting up doses and subjects for paitents with normal renal function

Based on the table above, the first three dosing regimens can be generated as follows:

**40 mg IV loading dose over 1 hour followed by 80 mg BID maintenance dose**

```julia
reg_IVPO_80 = DosageRegimen([40,80,80], cmt=[2,1,1],
                            time=[0,1,12], ii=[0,0,12],
                            addl=[0,0,4], rate=[40/1,0,0])
reg_PO_80 = DosageRegimen(80, cmt=1, time=0, ii=12, addl=5, rate=0)
s3_po = Subject(id=3, evs=reg_PO_80, cvs = (BW = 70, CKI=0))
s3 = Subject(id=3, evs=reg_IVPO_80, cvs=(BW = 70, CKI=0, dose=["iv1","po","po"], reg="IV_PO_80mg"))
```

```julia
reg1_po=simobs(sotalol_PKPD_model, s3_po, parameters_PKPD, randeffs)
reg1=simobs(sotalol_PKPD_model, s3, parameters_PKPD,randeffs)
plot(reg1, obsnames = [:cp,:qtc],
    color=[2 3],lw=2,
    xticks = 0:12:96, title = "Subject 3 - Regimen 1")
plot!(reg1_po, obsnames = [:cp,:qtc],
    color=[4],lw=2, linestyle = [:dot],
    xticks = 0:12:96, title = "Subject 3 - Regimen 1")
```


**40 mg IV loading dose over 1 hour followed by 20 mg IV for 30 minutes followed by 80 mg BID maintenance dose**

```julia
reg_IVPO_120 = DosageRegimen([40,20,120,120], cmt=[2,2,1,1],
                            time=[0,1,1.5,12],ii=[0,0,0,12],
                            addl=[0,0,0,4], rate=[40/1,20/0.5,0,0])
reg_PO_120 = DosageRegimen(120, cmt=1, time=0, ii=12, addl=5, rate=0)
s4_po = Subject(id=4, evs=reg_PO_120, cvs = (BW = 70, CKI=0))
s4 = Subject(id=4, evs=reg_IVPO_120, cvs=(BW = 70, CKI=0, dose=["iv1","iv2","po","po"]))
```

```julia
reg2_po=simobs(sotalol_PKPD_model, s4_po, parameters_PKPD, randeffs)
reg2=simobs(sotalol_PKPD_model, s4, parameters_PKPD,randeffs)
plot(reg2, obsnames = [:cp,:qtc],
    color=[2 3],lw=2,
    xticks = 0:12:96, title = "Subject 4 - Regimen 2")
plot!(reg2_po, obsnames = [:cp,:qtc],
    color=[4],lw=2, linestyle = [:dot],
    xticks = 0:12:96, title = "Subject 4 - Regimen 2")
```

**40 mg IV loading dose over 1 hour followed by two 20 mg IV for 30 minutes each followed by 80 mg BID maintenance dose**

```julia
reg_IVPO_160 = DosageRegimen([40,20,20,160,160], cmt=[2,2,2,1,1],
                            time=[0,1,1.5,2,12],ii=[0,0,0,0,12],
                            addl=[0,0,0,0,4], rate=[40/1,20/0.5,20/0.5,0,0])
reg_PO_160 = DosageRegimen(160, cmt=1, time=0, ii=12, addl=5, rate=0)
s5_po = Subject(id=5, evs=reg_PO_160, cvs = (BW = 70, CKI=0))
s5 = Subject(id=5, evs=reg_IVPO_160, cvs=(BW = 70, CKI=0, dose=["iv1","iv2","iv3","po","po"]))
```

```julia
reg3_po=simobs(sotalol_PKPD_model, s5_po, parameters_PKPD, randeffs)
reg3=simobs(sotalol_PKPD_model, s5, parameters_PKPD,randeffs)
plot(reg3, obsnames = [:cp,:qtc],
    color=[2 3],lw=2,
    xticks = 0:12:96, title = "Subject 5 - Regimen 3")
plot!(reg3_po, obsnames = [:cp,:qtc],
    color=[4],lw=2, linestyle = [:dot],
    xticks = 0:12:96, title = "Subject 5 - Regimen 3")
```

You can see from all three plots above that the dotted lines that represent the regular oral dosing take
three days to reach steady-state at their target dose. But the combination of IV and oral doses together gets
the patient to steady-state in less than 6 hours usually. This will enable the patients to be monitored only during
day 1 wit the potential to be discharged. The cost savings associated with this early discharge are discussed in
the manuscript.

This tutorial dose not cover the simulations for the chronic kidney disease patients. The only difference here
is that the doses are given once daily instead of two times a day.

## Impact of population variablity on QTc prolongation.

We can now simulate a population of 1000 subjects and evaluate the extremes of the QTc prolongation. Since we are
interested in the worst case scenario with respect to the maximum QTc prolongation, we can simulated the highest dose
regimen.


```julia
pop5 = Population(map(i -> Subject(id=i, evs=reg_IVPO_160, cvs=(BW = round.(rand(Normal(70, 10))), CKI=0, dose=["iv1","iv2","iv3","po","po"])), 1:1000))
sim_pop=simobs(sotalol_PKPD_model, pop5, parameters_PKPD, parallel_type=Pumas.Serial)
plot(sim_pop, obsnames = [:cp,:qtc],
    color=[2 3],lw=0.025,seriesalpha =0.2,
    xticks = 0:12:96, title = "Population Simulation - Regimen 3")
```

However, our interest is to evaluate if the change from baseline QTc is greater than 30 msec

```julia
simdf = DataFrame(sim_pop, include_events=false, include_covariates=false)
foo = by(simdf, :id, :cp => maximum)
bar = join(simdf, foo, on = :id)
```
